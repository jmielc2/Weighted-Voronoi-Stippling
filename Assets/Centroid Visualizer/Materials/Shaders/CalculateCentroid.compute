#pragma kernel Condense
#pragma kernel Reduce
#pragma use_dxc

Texture2D<float4> _VoronoiDiagram;
RWStructuredBuffer<float4x4> _PositionMatrixBuffer;
RWStructuredBuffer<float3> _ColorBuffer;
RWStructuredBuffer<float3> _WaveBuffer;

uniform uint _NumRegions;
uniform uint _ImageWidth, _ImageHeight;
uniform float _Width, _Height;
uniform uint _NumGroupsPerDispatch, _Stride, _Base, _Remaining;

groupshared float3 data[1024];

float2 PixelToWorldCoord(float2 pixel) {
    return float2(
        (((pixel.x + 0.5) / _ImageWidth) - 0.5) * _Width,
        (((pixel.y + 0.5) / _ImageHeight) - 0.5) * _Height
    );
}

// Centroid Algorithm
/*
Part One: Condense
2) Ballot wave front with predicate that checks if pixelVoronoiId == voronoiId.
3) Divide voronoiPixelIndex by count for pixels where pixelVoronoiId == voronoiId
4) Sum dividends using WaveActiveSum and write sum into waveBuffer
5) AllMemoryBarrierWithGroupSync to guarantee all waves have completed

Part Two: Reduce
6) id.y now represents waveId.
7) Reduce all warpSums to single value using WaveActiveSum and parallel reduction technique.
8) warpSum represents average voronoiPixelIndex for voronoi with ID of voronoiId, so convert to float2 to get centroid.
*/

[numthreads(1, 32, 1)]
void Condense(uint3 id : SV_DispatchThreadID, uint3 gid : SV_GroupId) {
    if (id.y < _ImageWidth * _ImageHeight) {
        uint2 pixelPosition = uint2(id.y, id.y / _ImageWidth);
        pixelPosition.x -= pixelPosition.y * _ImageWidth;
        const uint pixelVoronoiId = floor(_VoronoiDiagram[pixelPosition].r * _NumRegions + 0.5);
        const bool isActive = (pixelVoronoiId == _Base + id.x);
        const uint count = WaveActiveCountBits(isActive);
        if (!isActive) {
            if (WaveIsFirstLane() && count == 0) {
                _WaveBuffer[(id.x * _NumGroupsPerDispatch) + gid.y] = float3(0, 0, 0);
            }
            return;
        }
        float2 centroidPos = float2(0, 0);
        float2 worldPos = PixelToWorldCoord(pixelPosition);
        centroidPos.x = WaveActiveSum(worldPos.x);
        centroidPos.y = WaveActiveSum(worldPos.y);
        if (WaveIsFirstLane()) {
            _WaveBuffer[(id.x * _NumGroupsPerDispatch) + gid.y] = float3(centroidPos, count);
        }
    }
}

// https://developer.download.nvidia.com/assets/cuda/files/reduction.pdf
[numthreads(1, 1024, 1)]
void Reduce(uint3 id : SV_DispatchThreadID, uint3 tid : SV_GroupThreadId, uint3 gid : SV_GroupID) {
    if (id.y < _Remaining) {
        data[tid.y] = _WaveBuffer[(id.x * _NumGroupsPerDispatch) + (id.y * _Stride)];
    } else {
        data[tid.y] = float3(0, 0, 0);
    }
    AllMemoryBarrierWithGroupSync();
    
    for (int s = 512; s > 0; s >>= 1)
    {
        if (tid.y < s)
        {
            data[tid.y] += data[tid.y + s];
        }
        AllMemoryBarrierWithGroupSync();
    }

    if (tid.y == 0) {
        if (_Stride * 1024 < _NumGroupsPerDispatch) {
            _WaveBuffer[(id.x * _NumGroupsPerDispatch) + (gid.y * _Stride * 1024)] = data[0];
        } else {
            float3 centroidData = data[0];
            _PositionMatrixBuffer[_Base + id.x]._m03_m13 = centroidData.xy / centroidData.z;
        }
    }
}
